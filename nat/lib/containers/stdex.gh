#ifndef STDEX_GH
#define STDEX_GH

#include <listex.gh>

lemma void length_0_nil<T>(list<T> lst)
requires length(lst) == 0;
ensures lst == nil;
{
    switch (lst) {
    case nil: return;
    case cons(h, t):
        assert(length(lst) > length(t));
        assert(length(t) == 0);
        assert(length(lst) > 0);
        assert(length(lst) == 0);
        return;
    }
}

  
lemma void nth_0_head<t>(list<t> lst)
requires lst != nil;
ensures nth(0, lst) == head(lst);
{
    switch(lst) {
    case nil: return;
    case cons(h, t): return;
    }
}
  
lemma void nth_cons<T>(int n, list<T> lst, T head)
requires 0 < n;
ensures nth(n-1, lst) == nth(n, cons(head, lst));
{
    switch(lst) {
    case nil: return;
    case cons(h, t): return;
    }
}
  
lemma void nth_len_append_cons<T>(list<T> lst, T x)
requires true;
ensures nth(length(lst), append(lst, cons(x, nil))) == x;
{
    switch(lst) {
    case nil: return;
    case cons(h, t):
        nth_len_append_cons(t, x);
        return;
    }
}

lemma void nth_less_append_cons<T>(int n, list<T> lst, T x)
requires 0 <= n &*& n < length(lst);
ensures nth(n, append(lst, cons(x, nil))) == nth(n, lst);
{
    switch(lst) {
    case nil: return;
    case cons(h, t):
        if (n == 0) {
            assert(h == nth(n, lst));
            assert(h == nth(n, append(lst, cons(x, nil))));
            return;
        } else {
            nth_less_append_cons(n - 1, t, x);
            return;
        }
    }
}
  
lemma void append_take_cons<T>(list<T> lst)
requires lst != nil;
ensures lst == append(take(length(lst) - 1, lst), cons(nth(length(lst) - 1, lst), nil));
{
    switch(lst) {
    case nil: return;
    case cons(h, t):
        if (t == nil) {
            assert(length(lst) == 1);
            assert(take(length(lst) - 1, lst) == nil);
            assert(cons(nth(length(lst) - 1, lst), nil) == lst);
            assert(append(nil, lst) == lst);
            return;
        } else {
            append_take_cons(t);
            assert(take(length(lst) - 1, lst) == cons(h, take(length(t) - 1, t)));
            assert(nth(length(lst) - 1, lst) == nth(length(t) - 1, t));
            assert(append(take(length(lst) - 1, lst), cons(nth(length(lst) - 1, lst), nil)) ==
                   cons(h, append(take(length(t) - 1, t), cons(nth(length(t) - 1, t), nil))));
            return;
        }
    }
}


lemma void cons_take_take_cons<T>(T head, list<T> tail, int n)
requires 0 <= n;
ensures cons(head, take(n, tail)) == take(n + 1, cons(head, tail));
{
    switch(tail) {
    case nil: return;
    case cons(h, t):
        return;
    }
}

  
lemma void cons_head_tail<T>(list<T> lst)
requires lst != nil;
ensures lst == cons(head(lst), tail(lst));
{
    switch(lst){
    case nil: return;
    case cons(h, t): return;
    }
}
  
lemma void length_tail<T>(list<T> lst)
requires lst != nil;
ensures length(lst) == 1 + length(tail(lst)) &*& 0 < length(lst);
{
    switch(lst) {
    case nil: return;
    case cons(h,t): return;
    }
}

lemma void update_tail_tail_update<T>(T lhead, list<T> ltail, int index, T val)
requires 0 < index;
ensures tail(update(index, val, cons(lhead, ltail))) == update(index - 1, val, ltail);
{
  switch(ltail) {
    case nil: return;
    case cons(h, t):
      if (index - 1 == 0) {
      } else {
        update_tail_tail_update(h, t, index - 1, val);
        assert(update(index - 1, val, ltail) == cons(h, update(index - 2, val, t)));
        assert(update(index, val, cons(lhead, ltail)) == cons(lhead, update(index - 1, val, ltail)));
      }
      return;
  }
}

  lemma void tail_of_update_0<t>(list<t> lst, t val)
  requires lst != nil;
  ensures tail(lst) == tail(update(0, val, lst));
  {
    switch(lst) {
      case nil: break;
      case cons(h,t): break;
    }
  }

lemma void update_id<T>(int index, list<T> lst)
requires true;
ensures lst == update(index, nth(index, lst), lst);
{
  switch(lst){
    case nil: return;
    case cons(h, t):
      if (index == 0) {
      } else {
        update_id(index - 1, t);
      }
      return;
  }
}

  lemma void update_non_nil<t>(list<t> lst, int n, t val)
  requires lst != nil;
  ensures update(n, val, lst) != nil;
  {
    switch(lst) {
      case nil: break;
      case cons(h,t): break;
    }
  }

  lemma void head_update_0<t>(t val, list<t> lst)
  requires lst != nil;
  ensures head(update(0, val, lst)) == val;
  {
    switch(lst) {
      case nil:
      case cons(h,t):
    }
  }

  lemma void head_update_nonzero<t>(int n, t val, list<t> lst)
  requires 0 < n;
  ensures head(update(n, val, lst)) == head(lst);
  {
    switch(lst) {
      case nil:
      case cons(h,t):
    }
  }

  lemma void update_irrelevant_cell<t>(t vsearch, int n, t vnew, list<t> l)
  requires false == mem(vsearch, l) &*& vsearch != vnew;
  ensures false == mem(vsearch, update(n, vnew, l));
  {
    switch(l) {
      case nil: break;
      case cons(h,t):
        if (n == 0) {
        } else {
          update_irrelevant_cell(vsearch, n-1, vnew, t);
        }
    }
  }

lemma void drop_update_relevant<t>(int k, int i, t val, list<t> l)
requires 0 <= k &*& k <= i &*& i < length(l);
ensures drop(k, update(i, val, l)) == update(i-k, val, drop(k, l));
{
  switch(l) {
    case nil: return;
    case cons(h,t):
      if (k == 0) return;
      drop_update_relevant(k-1, i-1, val, t);
  }
}

lemma void drop_update_unrelevant<t>(int k, int i, t val, list<t> l)
requires 0 <= i &*& i < k;
ensures drop(k, update(i, val, l)) == drop(k, l);
{
  switch(l) {
    case nil: return;
    case cons(h,t):
      if (i == 0) return;
      drop_update_unrelevant(k-1, i-1, val, t);
  }
}

lemma void take_update_relevant<t>(int k, int i, t val, list<t> l)
requires 0 <= i &*& i <= k &*& k < length(l);
ensures take(k, update(i, val, l)) == update(i, val, take(k, l));
{
  switch(l) {
    case nil: return;
    case cons(h,t):
      if (i == 0) return;
      take_update_relevant(k-1, i-1, val, t);
  }
}

lemma void take_update_unrelevant<t>(int k, int i, t val, list<t> l)
requires 0 <= k &*& k <= i;
ensures take(k, update(i, val, l)) == take(k, l);
{
  switch(l) {
    case nil: return;
    case cons(h,t):
      if (k == 0) return;
      take_update_unrelevant(k-1, i-1, val, t);
  }
}

lemma void forall_update<t>(list<t> lst, fixpoint(t,bool) prop, int i, t v)
requires true == forall(lst, prop) &*& true == prop(v);
ensures true == forall(update(i, v, lst), prop);
{
  switch(lst) {
    case nil: return;
    case cons(h,t):
      if (i != 0) forall_update(t, prop, i-1, v);
  }
}

lemma void update_update<t>(list<t> lst, int i, t vi, int j, t vj)
requires i != j;
ensures update(i, vi, update(j, vj, lst)) == update(j, vj, update(i, vi, lst));
{
  switch(lst) {
    case nil: return;
    case cons(h,t):
      if (i == 0) return;
      if (j == 0) return;
      update_update(t, i-1, vi, j-1, vj);
  }
}

  lemma void nth_update_unrelevant<t>(int i, int j, t y, list<t> xs)
  requires i != j;
  ensures nth(i, update(j, y, xs)) == nth(i, xs);
  {
    switch(xs){
      case nil: break;
      case cons(h,t):
        if (j == 0) break;
        if (i == 0) break;
        nth_update_unrelevant(i-1, j-1, y, t);
    }
  }

fixpoint t get_some<t>(option<t> x) {
  switch(x) {
    case none: return default_value<t>();
    case some(v): return v;
  }
}

lemma void some_get_some<t>(option<t> x)
requires x != none;
ensures some(get_some(x)) == x;
{
  switch(x) {
    case none: break;
    case some(v): break;
  }
}

lemma void tail_drop<t>(list<t> lst, int n)
requires 0 <= n &*& n < length(lst);
ensures drop(n+1, lst) == tail(drop(n, lst));
{
  switch(lst) {
    case nil: return;
    case cons(h,t):
      if (n == 0) {
        return;
      } else {
        tail_drop(t, n-1);
      }
  }
}

  lemma void reverse_cons<t>(t head, list<t> tail)
  requires true;
  ensures reverse(cons(head, tail)) == append(reverse(tail), cons(head, nil));
  {
    reverse_append(reverse(tail), cons(head, nil));
  }

  lemma void append_reverse_take_cons<t>(int n, t head, list<t> tail,
                                         list<t> tt)
  requires 0 < n;
  ensures append(reverse(take(n-1, tail)), cons(head, tt)) ==
          append(reverse(take(n,cons(head, tail))), tt);
  {
    reverse_cons(head, take(n-1, tail));
    append_assoc(reverse(take(n-1, tail)), cons(head, nil), tt);
  }

  lemma void append_reverse_tail_cons_head<t>(list<t> l1, list<t> l2)
  requires l1 != nil;
  ensures append(reverse(tail(l1)), cons(head(l1), l2)) ==
          append(reverse(l1), l2);
  {
    reverse_cons(head(l1), tail(l1));
    cons_head_tail(l1);
    append_assoc(reverse(tail(l1)), cons(head(l1), nil), l2);
  }

  lemma void append_append_cons_to_append_cons<t>(list<t> l1, t el, list<t> l2)
  requires true;
  ensures append(append(l1, cons(el, nil)), l2) == append(l1, cons(el, l2));
  {
    switch(l1) {
      case nil: return;
      case cons(h,t):
        append_append_cons_to_append_cons(t, el, l2);
    }
  }


  lemma void forall_nth<t>(list<t> lst, fixpoint(t, bool) p, int i)
  requires 0 <= i &*& i < length(lst) &*& true == forall(lst, p);
  ensures true == p(nth(i, lst));
  {
    switch(lst) {
      case nil: return;
      case cons(h,t):
        if (i == 0){}
        else {
          forall_nth(t, p, i-1);
        }
    }
  }

lemma void forall_remove<t>(list<t> lst, t x, fixpoint(t, bool) p)
requires true == forall(lst, p);
ensures true == forall(remove(x, lst), p);
{
  switch(lst) {
    case nil: return;
    case cons(h,t):
      if (h == x) {
      } else {
        forall_remove(t, x, p);
      }
  }
}

  lemma void drop_drop<t>(int n, int m, list<t> l)
  requires 0 <= n &*& 0 <= m &*& n + m <= length(l);
  ensures drop(n, drop(m, l)) == drop(n + m, l);
  {
    switch(l) {
      case nil: return;
      case cons(h,t):
        if (n == 0) return;
        if (m == 0) return;
        drop_drop(n, m-1, t);
    }
  }

lemma void append_nonnil_l<t>(list<t> a, list<t> b)
requires a != nil;
ensures append(a, b) != nil;
{
  switch(a) {
    case nil: return;
    case cons(h,t):
      return;
  }
}

  lemma void filter_no_increase_len<t>(fixpoint (t, bool) f, list<t> xs)
  requires true;
  ensures length(filter(f, xs)) <= length(xs);
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        filter_no_increase_len(f, t);
    }
  }

  lemma void map_preserves_length<a,b>(fixpoint (a,b) f, list<a> xs)
  requires true;
  ensures length(map(f, xs)) == length(xs);
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        map_preserves_length(f, t);
    }
  }
  
  lemma void take_effect_on_len<t>(list<t> l, int n)
  requires 0 <= n;
  ensures (n <= length(l) ?
            (length(take(n, l)) == n) :
            (length(take(n, l)) == length(l)));
  {
    switch(l) {
      case nil: return;
      case cons(h,t):
        if (n != 0) take_effect_on_len(t, n-1);
    }
  }

  lemma void map_effect_on_len<a,b>(list<a> l, fixpoint (a,b) f)
  requires true;
  ensures length(l) == length(map(f, l));
  {
    switch(l) {
      case nil: return;
      case cons(h,t):
        map_effect_on_len(t, f);
    }
  }

  lemma void filter_effect_on_len<t>(list<t> l, fixpoint (t,bool) f)
  requires true;
  ensures (length(filter(f, l)) == length(l) ?
            (filter(f, l) == l) :
            (length(filter(f, l)) < length(l)));
  {
    switch(l) {
      case nil: return;
      case cons(h,t):
        filter_effect_on_len(t, f);
    }
  }

  lemma void car_drop_is_nth<t>(int n, list<t> lst)
  requires 0 <= n &*& n < length(lst) &*& drop(n, lst) == cons(?head,_);
  ensures nth(n, lst) == head;
  {
    switch(lst) {
      case nil: return;
      case cons(h,t):
        if (0 < n) car_drop_is_nth(n-1, t);
    }
  }

  lemma void take_take<t>(int n, int m, list<t> lst)
  requires 0 <= n &*& n <= m;
  ensures take(n, take(m, lst)) == take(n, lst);
  {
    switch(lst) {
      case nil: return;
      case cons(h,t):
        if (0 < n) take_take(n-1, m-1, t);
    }
  }

  lemma void take_map<a,b>(int n, fixpoint (a,b) f, list<a> lst)
  requires 0 <= n &*& n < length(lst);
  ensures take(n, map(f, lst)) == map(f, take(n, lst));
  {
    switch(lst) {
      case nil: return;
      case cons(h,t):
        if (0 < n) take_map(n-1, f, t);
    }
  }

  lemma void remove_nonmem<t>(t el, list<t> l)
  requires false == mem(el, l);
  ensures remove(el, l) == l;
  {
    switch(l) {
      case nil: return;
      case cons(h,t):
        if (h != el) remove_nonmem(el, t);
    }
  }

  lemma void distinct_unique<t>(list<t> lst, t x)
  requires true == distinct(lst);
  ensures false == mem(x, remove(x, lst));
  {
    switch(lst) {
      case nil: return ;
      case cons(h,t):
        if (h != x) distinct_unique(t, x);
    }
  }

  lemma void remove_unrelevant_preserves_distinct<t>(list<t> l, t x)
  requires false == mem(x, remove(x, l));
  ensures distinct(l) == distinct(remove(x, l));
  {
    switch(l) {
      case nil:
      case cons(h,t):
        if (h != x) {
          neq_mem_remove(h, x, t);
          remove_unrelevant_preserves_distinct(t, x);
        }
    }
  }

  lemma void remove_still_distinct<t>(list<t> l, t x)
  requires true == distinct(l);
  ensures true == distinct(remove(x, l));
  {
    switch(l) {
      case nil:
      case cons(h,t):
        if (h != x) {
          neq_mem_remove(h, x, t);
          remove_still_distinct(t, x);
        }
    }
  }

  lemma void drop_append_small<t>(int n, list<t> l1, list<t> l2)
  requires 0 <= n &*& n <= length(l1);
  ensures append(drop(n, l1), l2) == drop(n, append(l1, l2));
  {
    switch(l1) {
      case nil: return;
      case cons(h, t):
        if (n == 0) return;
        drop_append_small(n-1, t, l2);
    }
  }

  lemma void take_append_small<t>(int n, list<t> l1, list<t> l2)
  requires 0 <= n &*& n <= length(l1);
  ensures take(n, l1) == take(n, append(l1, l2));
  {
    switch(l1) {
      case nil: return;
      case cons(h, t):
        if (n == 0) return;
        take_append_small(n-1, t, l2);
    }
  }

  lemma void head_take<t>(int n, list<t> l)
  requires 0 < n;
  ensures head(l) == head(take(n, l));
  {
    switch(l) {
      case nil: return;
      case cons(h,t):
        if (n == 1) return;
        else head_take(n-1, t);
    }
  }

  lemma void nonmem_map_filter<t1,t2>(fixpoint (t1,t2) f1,
                                      fixpoint (t1,bool) f2,
                                      list<t1> l,
                                      t1 el)
  requires false == mem(f1(el), map(f1, l));
  ensures false == mem(f1(el), map(f1, filter(f2, l)));
  {
    switch(l) {
      case nil:
      case cons(h,t):
        nonmem_map_filter(f1, f2, t, el);
    }
  }

  lemma void distinct_map_filter<t1,t2>(fixpoint (t1,t2) f1,
                                        fixpoint (t1,bool) f2,
                                        list<t1> l)
  requires true == distinct(map(f1, l));
  ensures true == distinct(map(f1, filter(f2, l)));
  {
    switch(l) {
      case nil:
      case cons(h,t):
        nonmem_map_filter(f1, f2, t, h);
        distinct_map_filter(f1, f2, t);
    }
  }

  lemma void non_mem_map_remove<t1,t2>(list<t1> l, t2 x1, t1 x2,
                                       fixpoint (t1,t2) f)
  requires false == mem(x1, map(f, l));
  ensures false == mem(x1, map(f, remove(x2, l)));
  {
    switch(l) {
      case nil:
      case cons(h,t):
        if (h != x2) {
          non_mem_map_remove(t, x1, x2, f);
        }
    }
  }


  lemma void distinct_map_remove<t1,t2>(list<t1> l, fixpoint (t1,t2) f, t1 x)
  requires true == distinct(map(f, l));
  ensures true == distinct(map(f, remove(x, l)));
  {
    switch(l) {
      case nil:
      case cons(h,t):
        if (x != h) {
          assert false == mem(f(h), map(f, t));
          non_mem_map_remove(t, f(h), x, f);
          assert false == mem(f(h), map(f, remove(x, t)));
          distinct_map_remove(t, f, x);
        }
    }
  }



  lemma void forall_filter<t>(fixpoint (t,bool) f1,
                              fixpoint (t,bool) f2,
                              list<t> l)
  requires true == forall(l, f1);
  ensures true == forall(filter(f2, l), f1);
  {
    switch(l) {
      case nil:
      case cons(h,t):
        forall_filter(f1, f2, t);
    }
  }

  lemma void filter_append_idemp<t>(list<t> l1, list<t> l2,
                                    fixpoint (t,bool) f)
  requires true;
  ensures filter(f, append(l1, l2)) == append(filter(f, l1), filter(f, l2));
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        filter_append_idemp(t, l2, f);
    }
  }

  lemma void filter_forall<t>(fixpoint (t,bool) f, list<t> l)
  requires true;
  ensures true == (l == filter(f, l)) == forall(l, f) &*&
          true == forall(filter(f, l), f);
  {
    switch(l) {
      case nil:
      case cons(h,t):
        filter_forall(f, t);
        if (f(h)) {
        } else {
          assert false == forall(l, f);
          assert filter(f,l) == filter(f,t);
          filter_no_increase_len(f, t);
          assert length(filter(f, l)) < length(l);
          assert l != filter(f,l);
        }
    }
  }

  lemma void double_mem_append_nondistinct<t>(list<t> l1, list<t> l2, t x)
  requires true == mem(x, l1) &*& true == mem(x, l2);
  ensures false == distinct(append(l1, l2));
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        if (h == x) {
          mem_append(x, t, l2);
        } else {
          double_mem_append_nondistinct(t, l2, x);
        }
    }
  }

  lemma void distinct_unappend<t>(list<t> l1, list<t> l2)
  requires true == distinct(append(l1,l2));
  ensures true == distinct(l1) &*& true == distinct(l2);
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        mem_append(h, t, l2);
        distinct_unappend(t, l2);
    }
  }

  lemma void append_remove_first<t>(list<t> l1, list<t> l2, t el)
  requires true == mem(el, l1);
  ensures remove(el, append(l1, l2)) == append(remove(el, l1), l2);
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        if (h != el) append_remove_first(t, l2, el);
    }
  }

//-----===== Set lemmas =====-----

  lemma void subset_remove<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(xs, ys);
  ensures true == subset(remove(el, xs), ys);
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        if (h != el) subset_remove(t, ys, el);
    }
  }

  lemma void remove_subset<t>(list<t> xs, t el, list<t> ys)
  requires true == subset(xs, ys) &*&
           false == mem(el, xs);
  ensures true == subset(xs, remove(el, ys));
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        neq_mem_remove(h, el, ys);
        remove_subset(t, el, ys);
    }
  }

  lemma void remove_both_subset<t>(t el, list<t> xs, list<t> ys)
  requires true == subset(xs, ys) &*&
           false == mem(el, remove(el, xs));
  ensures true == subset(remove(el, xs), remove(el, ys));
  {
    subset_remove(xs, ys, el);
    remove_subset(remove(el, xs), el, ys);
  }

  lemma void subset_remove_unrelevant<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(xs, ys) &*& false == mem(el, xs);
  ensures true == subset(xs, remove(el, ys));
  {
    remove_nonmem(el, xs);
    remove_both_subset(el, xs, ys);
  }

  lemma void subset_append<t>(list<t> xs, list<t> ys, list<t> zs)
  requires true == subset(xs, zs) &*& true == subset(ys, zs);
  ensures true == subset(append(xs, ys), zs);
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        subset_append(t, ys, zs);
    }
  }

  lemma void subset_append2<t>(list<t> xs, list<t> ys, list<t> zs)
  requires true == subset(xs, ys);
  ensures true == subset(xs, append(ys, zs));
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        subset_append2(t, ys, zs);
    }
  }

  lemma void subset_append3<t>(list<t> xs, list<t> ys, list<t> zs)
  requires true == subset(xs, zs);
  ensures true == subset(xs, append(ys, zs));
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        subset_append3(t, ys, zs);
    }
  }

  lemma void remove_extra_preserves_subset<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(xs, ys) &*& true == mem(el, remove(el, ys));
  ensures true == subset(xs, remove(el, ys));
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        if (h != el) neq_mem_remove(h, el, ys);
        remove_extra_preserves_subset(t, ys, el);
    }
  }

  lemma void add_extra_preserves_subset<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(xs, ys);
  ensures true == subset(xs, cons(el, ys));
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        add_extra_preserves_subset(t, ys, el);
    }
  }

  lemma void subset_unremove<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(remove(el, xs), ys);
  ensures true == subset(xs, cons(el, ys));
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        if (h != el) subset_unremove(t, ys, el);
        else add_extra_preserves_subset(t, ys, el);
    }
  }

  lemma void subset_mem_trans<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(xs, ys) &*& true == mem(el, xs);
  ensures true == mem(el, ys);
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        if (h != el) subset_mem_trans(t, ys, el);
    }
  }


  lemma void distinct2_subset_sublen<t>(list<t> xs, list<t> ys)
  requires true == distinct(xs) &*&
           true == subset(xs, ys);
  ensures length(xs) <= length(ys);
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        subset_remove_unrelevant(t, ys, h);
        distinct2_subset_sublen(t, remove(h, ys));
    }
  }

  lemma list<t> distinct_superset<t>(list<t> xs, list<t> ys)
  requires true == subset(xs, ys) &*&
           true == distinct(xs);
  ensures true == subset(xs, result) &*& true == subset(result, ys) &*&
          true == distinct(result) &*& length(result) <= length(ys);
  {
    switch(ys) {
      case nil: return nil;
      case cons(h,t):
        if (mem(h, t)) {
          remove_extra_preserves_subset(xs, ys, h);
          list<t> rez = distinct_superset(xs, t);
          add_extra_preserves_subset(rez, t, h);
          return rez;
        }
        else {
          distinct_unique(xs, h);
          remove_both_subset(h, xs, ys);
          distinct_remove(h, xs);
          list<t> t_rez = distinct_superset(remove(h, xs), t);
          subset_unremove(xs, t_rez, h);
          add_extra_preserves_subset(t_rez, t, h);
          if (true == mem(h, t_rez)) {
            subset_mem_trans(t_rez, t, h);
          }
          assert false == mem(h, t_rez);
          return cons(h, t_rez);
        }
    }
  }

  lemma void distinct_subset_sublen<t>(list<t> xs, list<t> ys)
  requires true == distinct(xs) &*&
           true == subset(xs, ys);
  ensures length(xs) <= length(ys);
  {
    distinct2_subset_sublen(xs, distinct_superset(xs, ys));
  }

  lemma void subset_push_to_the_end<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(xs, ys);
  ensures true == subset(xs, append(remove(el, ys), cons(el, nil)));
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        if (h == el) {
        } else {
          neq_mem_remove(h, el, ys);
        }
        subset_push_to_the_end(t, ys, el);
    }
  }

  lemma void push_to_the_end_subset<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(xs, ys) &*& true == mem(el, ys);
  ensures true == subset(append(remove(el, xs), cons(el, nil)), ys);
  {
    subset_remove(xs, ys, el);
    subset_append(remove(el, xs), cons(el, nil), ys);
  }

  lemma void subset_forall<t>(list<t> xs, list<t> ys, fixpoint (t,bool) prop)
  requires true == subset(xs, ys) &*& true == forall(ys, prop);
  ensures true == forall(xs, prop);
  {
    switch(xs) {
      case nil: return;
      case cons(h,t):
        forall_mem(h, ys, prop);
        subset_forall(t, ys, prop);
    }
  }

  lemma void subset_append_distinct<t>(list<t> xs, list<t> ys, list<t> zs)
  requires true == subset(xs, ys) &*&
           true == distinct(xs) &*&
           true == distinct(append(ys, zs));
  ensures true == distinct(append(xs, zs));
  {
    switch(xs) {
      case nil:
        distinct_unappend(ys, zs);
      case cons(h,t):
        mem_append(h, t, zs);
        if (mem(h, zs)) {
          assert true == mem(h, ys);
          double_mem_append_nondistinct(ys, zs, h);
          assert false == distinct(append(ys, zs));
        }
        subset_append_distinct(t, ys, zs);
    }
  }

  lemma void subset_map<t1,t2>(list<t1> l1, list<t1> l2, fixpoint (t1,t2) f)
  requires true == subset(l1, l2);
  ensures true == subset(map(f, l1), map(f, l2));
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        mem_map(h, l2, f);
        subset_map(t, l2, f);
    }
  }

  lemma void filter_subset<t>(fixpoint (t,bool) f, list<t> l)
  requires true;
  ensures true == subset(filter(f, l), l);
  {
    switch(l) {
      case nil:
      case cons(h,t):
        filter_subset(f, t);
        if (f(h)) {
          assert remove(h, (filter(f, l))) == filter(f, t);
          subset_unremove(filter(f,l), t, h);
        } else {
          add_extra_preserves_subset(filter(f,l), t, h);
        }
    }
  }

  lemma void append_both_subset<t>(list<t> xs, list<t> ys, list<t> zs)
  requires true == subset(xs, zs);
  ensures true == subset(append(xs, ys), append(zs, ys)) &*&
          true == subset(append(xs, ys), append(ys, zs)) &*&
          true == subset(append(ys, xs), append(zs, ys)) &*&
          true == subset(append(ys, xs), append(ys, zs));
  {
    subset_append2(xs, zs, ys);
    subset_append3(xs, ys, zs);
    subset_refl(ys);
    subset_append3(ys, zs, ys);
    subset_append2(ys, ys, zs);
    subset_append(xs, ys, append(zs, ys));
    subset_append(xs, ys, append(ys, zs));
    subset_append(ys, xs, append(zs, ys));
    subset_append(ys, xs, append(ys, zs));
  }

  fixpoint bool set_eq<t>(list<t> l1, list<t> l2) {
    return subset(l1, l2) && subset(l2, l1);
  }

  lemma void set_eq_remove_uniq_both<t>(list<t> l1, list<t> l2, t x)
  requires true == set_eq(l1, l2) &*&
           false == mem(x, remove(x, l1)) &*&
           false == mem(x, remove(x, l2));
  ensures true == set_eq(remove(x, l1), remove(x, l2));
  {
    remove_both_subset(x, l1, l2);
    remove_both_subset(x, l2, l1);
  }

  lemma void set_eq_remove_both<t>(list<t> l1, list<t> l2, t x)
  requires true == set_eq(l1, l2) &*&
           true == distinct(l1) &*&
           true == distinct(l2);
  ensures true == set_eq(remove(x, l1), remove(x, l2));
  {
    distinct_unique(l1, x);
    distinct_unique(l2, x);
    set_eq_remove_uniq_both(l1, l2, x);
  }

  lemma void subset_nil_nil<t>(list<t> l)
  requires true == subset(l, nil);
  ensures l == nil;
  {
    switch(l) {
      case nil:
      case cons(h,t):
    }
  }

  lemma void set_eq_distinct_same_len<t>(list<t> l1,
                                         list<t> l2)
  requires true == set_eq(l1, l2) &*&
           true == distinct(l1) &*&
           true == distinct(l2);
  ensures length(l1) == length(l2);
  {
    switch(l1) {
      case nil:
        subset_nil_nil(l2);
      case cons(h,t):
        set_eq_remove_both(l1, l2, h);
        distinct_remove(h, l2);
        set_eq_distinct_same_len(t, remove(h,l2));
    }
  }

  lemma void set_eq_forall_both<t>(list<t> l1, list<t> l2,
                                   fixpoint (t,bool) prop)
  requires true == set_eq(l1, l2);
  ensures forall(l1, prop) == forall(l2, prop);
  {
    if (forall(l1, prop)) {
      subset_forall(l2, l1, prop);
    }
    if (forall(l2, prop)) {
      subset_forall(l1, l2, prop);
    }
  }//took 4m

  lemma void content_eq_map<t1,t2>(list<t1> l1, list<t1> l2,
                                   fixpoint (t1,t2) f)
  requires true == set_eq(l1,l2);
  ensures true == set_eq(map(f, l1), map(f, l2));
  {
    subset_map(l1, l2, f);
    subset_map(l2, l1, f);
  }//took 1m

  lemma void distinct_shorter_than_nondistinct<t>(list<t> l1, list<t> l2)
  requires true == subset(l1, l2) &*&
           true == distinct(l1) &*&
           false == distinct(l2);
  ensures length(l1) < length(l2);
  {
    switch(l1) {
      case nil:
        switch(l2) {
          case nil:
            assert false;
          case cons(h,t):
            assert 0 < length(l2);
        }
      case cons(h,t):
        remove_subset(t, h, l2);
        if (mem(h, remove(h, l2))) {
          distinct_subset_sublen(l1, remove(h, l2));
        } else {
          remove_unrelevant_preserves_distinct(l2, h);
          distinct_shorter_than_nondistinct(t, remove(h, l2));
        }
    }
  }

  lemma void
  set_eq_same_len_distinct_both<t>(list<t> l1, list<t> l2)
  requires true == set_eq(l1, l2) &*& length(l1) == length(l2);
  ensures distinct(l1) == distinct(l2);
  {
    if (distinct(l1) && !distinct(l2)) {
      distinct_shorter_than_nondistinct(l1, l2);
    }
    if (!distinct(l1) && distinct(l2)) {
      distinct_shorter_than_nondistinct(l2, l1);
    }
  }

  fixpoint bool multiset_eq<t>(list<t> l1, list<t> l2) {
    switch(l1) {
      case nil:
        return l2 == nil;
      case cons(h,t):
        return true == mem(h, l2) && multiset_eq(t, remove(h, l2));
    }
  }

  lemma void multiset_eq_unremove<t>(list<t> l1, list<t> l2, t x)
  requires true == mem(x, l1) &*& 
           true == multiset_eq(remove(x, l1), l2);
  ensures true == multiset_eq(l1, cons(x, l2));
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        if (x != h) multiset_eq_unremove(t, remove(h, l2), x);
    }
  }

  lemma void multiset_eq_comm<t>(list<t> l1, list<t> l2)
  requires true == multiset_eq(l1, l2);
  ensures true == multiset_eq(l2, l1);
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        multiset_eq_comm(t, remove(h, l2));
        assert true == multiset_eq(remove(h, l2), t);
        multiset_eq_unremove(l2, t, h);
        assert true == multiset_eq(l2, l1);
    }
  }

  lemma void subset_unremove_outer<t>(list<t> xs, list<t> ys, t el)
  requires true == subset(xs, remove(el, ys));
  ensures true == subset(xs, ys);
  {
    switch(xs) {
      case nil:
      case cons(h,t):
        if (h!=el)
          neq_mem_remove(h, el, t);
        subset_unremove_outer(t, ys, el);
        mem_remove_mem(h, el, ys);
    }
  }

  lemma void multiset_eq_subset<t>(list<t> l1, list<t> l2)
  requires true == multiset_eq(l1, l2);
  ensures true == subset(l1, l2);
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        multiset_eq_subset(t, remove(h, l2));
        assert true == subset(t, remove(h, l2));
        subset_unremove_outer(t, l2, h);
        assert true == subset(l1, l2);
    }
  }

  lemma void multiset_eq_set_eq<t>(list<t> l1, list<t> l2)
  requires true == multiset_eq(l1, l2);
  ensures true == set_eq(l1, l2);
  {
    multiset_eq_subset(l1, l2);
    multiset_eq_comm(l1, l2);
    multiset_eq_subset(l2, l1);
  }

  lemma void distinct_set_eq_multiset_eq<t>(list<t> l1, list<t> l2)
  requires true == set_eq(l1, l2) &*&
           true == distinct(l1) &*&
           true == distinct(l2);
  ensures true == multiset_eq(l1, l2);
  {
    switch(l1) {
      case nil:
        subset_nil_nil(l2);
      case cons(h,t):
        remove_still_distinct(l2, h);
        set_eq_remove_both(l1, l2, h);
        distinct_set_eq_multiset_eq(t, remove(h, l2));
        true == multiset_eq(t, remove(h, l2));
        assert true == mem(h, l2);
    }
  }

  lemma void multiset_eq_refl<t>(list<t> l)
  requires true;
  ensures true == multiset_eq(l, l);
  {
    switch(l) {
      case nil:
      case cons(h,t):
        multiset_eq_refl(t);
    }
  }

  lemma void cons_in_the_middle_multiset_eq<t>(list<t> l1, list<t> l2, t x)
  requires true;
  ensures true == multiset_eq(append(l1, cons(x, l2)), cons(x, append(l1, l2)));
  {
    switch(l1) {
      case nil:
        multiset_eq_refl(cons(x, l2));
      case cons(h,t):
        cons_in_the_middle_multiset_eq(t, l2, x);
    }
  }

  lemma void multiset_eq_same_len<t>(list<t> l1, list<t> l2)
  requires true == multiset_eq(l1, l2);
  ensures length(l1) == length(l2);
  {
    switch(l1) {
      case nil:
      case cons(h,t):
        length_remove(h, l2);
        multiset_eq_same_len(t, remove(h, l2));
    }
  }

  lemma void multiset_eq_append_both<t>(list<t> l1, list<t> l2, list<t> l3)
  requires true == multiset_eq(l1, l2);
  ensures true == multiset_eq(append(l1, l3),
                              append(l2, l3));
  {
    switch(l1) {
      case nil:
        multiset_eq_refl(l3);
      case cons(h,t):
        multiset_eq_append_both(t, remove(h, l2), l3);
        assert true == mem(h, append(l2, l3));
        append_remove_first(l2, l3, h);
        assert remove(h, append(l2, l3)) == append(remove(h, l2), l3);
    }
  }

// Workaround for unmindful Redux solver.
// See https://github.com/verifast/verifast/issues/19
lemma void note(bool b)
requires b;
ensures b;
{}

#endif//STDEX_GH
