#ifndef _PRIME_GH_INCLUDED_
#define _PRIME_GH_INCLUDED_

#include <nat.gh>
#include "modulo.gh"

predicate prime(int x) = is_prime(x) == true;
	
predicate coprime(int x, int y) = are_coprime(x, y) == true;

fixpoint int min(int x, int y) {
    return x < y ? x : y;
} 

fixpoint bool divide(int quotient, int divisor) {
    return (quotient/divisor)*divisor == quotient; 
}

fixpoint bool common_divide(int quotient1, int quotient2, int divisor) {
    return divide(quotient1, divisor) && divide(quotient2, divisor);
} 

fixpoint bool is_prime_div(int x, nat divisor) {
    switch(divisor) {
        case zero: return false;
        case succ(divisor_pred):
            return divisor_pred == zero 
                ? false
                : divisor_pred == succ(zero) 
                    ? !divide(x, int_of_nat(divisor))
                    : !divide(x, int_of_nat(divisor)) && is_prime_div(x, divisor_pred);
    }

}

fixpoint bool is_prime(int x) {
    return is_prime_div(x, nat_of_int(x));
}

fixpoint bool are_coprime_div(int x, int y, nat divisor) {
    switch(divisor) {
        case zero: return false;
        case succ(divisor_pred):
            return divisor_pred == zero
                ? false
                : divisor_pred == succ(zero)
                    ? !common_divide(x, y, int_of_nat(divisor))
                    : !common_divide(x, y, int_of_nat(divisor)) && are_coprime_div(x, y, divisor_pred);
    }

}

fixpoint bool are_coprime(int x, int y) {
    return are_coprime_div(x, y, nat_of_int(min(x,y)));
}

lemma void mul_nonzero(int a, int b)
    requires a > 0 &*& b > 0;
    ensures a * b > 0;
{
    mul_nonnegative(a - 1, b);
    assert (a-1)*b < a*b;
}

lemma void modulo_less_than(int k, int m)
    requires 0 <= k &*& k < m &*& 0 < m;
    ensures k % m == k;
{
    division_round_to_zero(k, m);
    div_rem_nonneg(k, m);
}

// Assumption !
lemma void modulo_mul_coprime(int k, int s, int m)
    requires
        0 < k &*& k < m &*&
        0 < s &*& s < m &*&
        0 < m &*& 
        (k % m != 0) &*& coprime(s, m);
    ensures
        (k * s) % m != 0 &*& coprime(s, m);
{
    assume ((k * s) % m != 0);
}

// Assumption !
lemma void modulo_mul_split(int a, int b, int m)
    requires
        0 <= a &*& 0 <= b &*& 
        0 < m &*& 
        (b - a) % m != 0;
    ensures
        b % m != a % m;
{
    assume (b % m != a % m);
}

lemma void modulo_permutation(int a, int b, int s, int m) 
    requires 
        0 <= a &*& a < m &*&
        0 <= b &*& b < m &*&
        0 < s &*& s < m &*&
        0 < m &*& 
        b > a &*& coprime(s, m);
    ensures
        (s * a) % m != (s * b) % m  &*& coprime(s, m) ;
{
    modulo_less_than(b - a, m);
    modulo_mul_coprime(b - a, s, m);
    assert ((s * (b - a)) % m != 0 );
    assert ((s * b - s * a) % m != 0 );
    mul_nonnegative(s, a);
    mul_nonnegative(s, b);
    modulo_mul_split(s * a, s * b, m);
    assert ( (s * b) % m != (s * a) % m );
} 

#endif //_PRIME_GH_INCLUDED_
